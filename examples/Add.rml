define add:

    define getLastCharacter:
		string {'.'}:char | {'.'}:char = char.  
        
    define getAllButLastCharacter:
		'.' = '';
		string {'.'}:char = string.  
        
    define appendDigitWithCarry:
	    9, 'c' space = 'c' 0 space;
	    1 a, 'c' space = 'c' inc(a) space;
	    1 a, $space = 'c' a space;
	    a, 'c' space = inc(a) space;
	    a, $space = a space.
        
    define padWithZeros:
        '', '', $result = result;
        '', m, $result = padWithZeros(
            '', 
            getAllButLastCharacter(m), 
            result 0
        );
        n, '', $result = n result;
        n, m, $result = padWithZeros(
            getAllButLastCharacter(n), 
            getAllButLastCharacter(m), 
            getLastCharacter(n) result
        );
        n, m = padWithZeros(n, m, '').
        
    define clearCarry:
        'c' space = '1' space;
        space = space.
        
    define naiveAdd:
        a, 0 = a;
        a, b = naiveAdd(inc(a), dec(b)).
        
    define addDecimals:
        whole, 'c' part = clearCarry(
            appendDigitWithCarry(
                clearCarry(whole), 'c.' part
            )
        );
        whole, part = clearCarry(whole) '.' part;
        a, b, c, d = addDecimals(
            add(a, b), 
            add(padWithZeros(c, d), padWithZeros(d, c))
        ).
        
    '', '', space = clearCarry(space);
    '', n, space | n, '', space = appendDigitWithCarry(n, space);
    n, m, $space = add(
        getAllButLastCharacter(n), 
        getAllButLastCharacter(m), 
        appendDigitWithCarry(
            naiveAdd(
                getLastCharacter(n), 
                getLastCharacter(m)
            ), 
            space
        )
    );
    '-'n, '-'m = '-'add(n, m);
    a '\.' b, c '\.' d = addDecimals(a, b, c, d);
    whole '\.' part, m | m, whole '\.' part1 = add(whole, m) '.' part;
    n, m = add(n, m, '').